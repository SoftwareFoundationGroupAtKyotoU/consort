apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

repositories {
  mavenCentral()
}

application {
	mainClassName = "edu.kyoto.fos.regnant.Regnant"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

sourceSets {
	runtime {
		java {
			srcDirs = ["src/runtime/java"]
		}
	}
	integration {
		java {
			srcDirs = [ "src/integration/java" ]
		}
		compileClasspath += sourceSets.runtime.output
		runtimeClasspath += sourceSets.runtime.output
	}
}

dependencies {
	implementation ("ca.mcgill.sable:soot:3.3.0") {
		exclude group: "org.ow2.asm", module: "asm-debug-all"
	}
	implementation group: 'org.ow2.asm', name: 'asm', version: '7.2'
	implementation group: 'org.ow2.asm', name: 'asm-util', version: '7.2'
	implementation group: 'org.ow2.asm', name: 'asm-commons', version: '7.2'
	implementation group: 'org.yaml', name: 'snakeyaml', version: '1.17'
	implementation "org.functionaljava:functionaljava:4.8"
}

task allDeps(type: Jar) {
    archiveBaseName = 'regnant-with-deps'
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task integrationJar(type: Jar, dependsOn: integrationClasses) {
	archiveBaseName = "integration"
	from sourceSets.integration.output
}

task runtimeJar(type: Jar, dependsOn: integrationClasses) {
	archiveBaseName = "runtime"
	from sourceSets.runtime.output
}


task generateWorkScript(type: CreateStartScripts) {
	mainClassName = "edu.kyoto.fos.regnant.GenerateWork"
	applicationName = "generateWork"
	outputDir = file("build/generate-work")
	classpath = startScripts.classpath
}

distributions {
	main {
		contents {
			from(generateWorkScript) {
				into 'bin'
			}
		}
	}
}

startScripts.defaultJvmOpts += ["-ea"]

assemble.dependsOn += integrationJar

compileIntegrationJava.targetCompatibility = JavaVersion.VERSION_1_8
compileIntegrationJava.sourceCompatibility = JavaVersion.VERSION_1_8

targetCompatibility = JavaVersion.VERSION_11
sourceCompatibility = JavaVersion.VERSION_11
